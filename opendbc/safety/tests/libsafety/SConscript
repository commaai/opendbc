import platform

# standard env
env = Environment(
  # use clang as GCC has a bit less coverage, seemingly
  # due to optimizing out dead branches before coverage analysis
  # see https://github.com/commaai/opendbc/pull/3002
  CC='clang',
  CFLAGS=[
    '-Wall',
    "-Wextra",
    '-Werror',
    '-nostdlib',
    '-fno-builtin',
    '-std=gnu11',
    '-Wfatal-errors',
    '-Wno-pointer-to-int-cast',
    '-g',
    '-O0',
    '-fno-omit-frame-pointer',
    '-fprofile-arcs',
    '-ftest-coverage',
    '-DALLOW_DEBUG',
  ],
  LINKFLAGS=[
    '-fprofile-arcs',
    '-ftest-coverage',
  ],
  CPPPATH=["#", "../../board/"],
  tools=["default", "compilation_db"],
)
if platform.system() == "Darwin":
  env.PrependENVPath('PATH', '/opt/homebrew/bin')
if GetOption('ubsan'):
  env.Prepend(LINKFLAGS=[
    "-fsanitize=undefined",
    "-fno-sanitize-recover=undefined",
  ])

# mutation env
menv = env.Clone()
if platform.system() == "Darwin":
  menv.PrependENVPath('PATH', '/opt/homebrew/opt/llvm@17/bin')
  mull_plugin = Dir('#').abspath + '/.mull/lib/mull-ir-frontend-17'
else:
  mull_plugin = '/usr/lib/mull-ir-frontend-17'
menv['CC'] = 'clang-17'
flags = [
  '-fprofile-instr-generate',
  '-fcoverage-mapping',
  f'-fpass-plugin={mull_plugin}',
  '-g',
  '-grecord-command-line',
]
menv['CFLAGS'] += flags
menv['LINKFLAGS'] = flags
menv['CFLAGS'].remove('-fprofile-arcs')
menv['CFLAGS'].remove('-ftest-coverage')

for build_env, suffix in ((env, ""), (menv, "_mutation")):
  safety = build_env.SharedObject(f"safety{suffix}.os", "safety.c")
  libsafety = build_env.SharedLibrary(f"libsafety{suffix}.so", [safety])

  # GCC note file is generated by compiler, allow scons to clean it up
  build_env.SideEffect("safety.gcno", safety)
