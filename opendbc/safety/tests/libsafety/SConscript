import platform

system = platform.system()

# Use clang-17 for both normal tests and mutation testing
# The Mull plugin injects mutations that are dormant unless run with mull-runner
if system == "Darwin":
  mull_plugin = Dir('#').abspath + '/.mull/lib/mull-ir-frontend-17'
else:
  mull_plugin = '/usr/lib/mull-ir-frontend-17'

env = Environment(
  CC='clang-17',
  CFLAGS=[
    '-Wall',
    "-Wextra",
    '-Werror',
    '-nostdlib',
    '-fno-builtin',
    '-std=gnu11',
    '-Wfatal-errors',
    '-Wno-pointer-to-int-cast',
    '-g',
    '-O0',
    '-fno-omit-frame-pointer',
    '-fprofile-arcs',
    '-ftest-coverage',
    f'-fpass-plugin={mull_plugin}',
    '-grecord-command-line',
    '-DALLOW_DEBUG',
  ],
  LINKFLAGS=[
    '-fprofile-arcs',
    '-ftest-coverage',
    '-fsanitize=undefined',
    '-fno-sanitize-recover=undefined',
  ],
  CPPPATH=["#"],
  tools=["default", "compilation_db"],
)
if system == "Darwin":
  env.PrependENVPath('PATH', '/opt/homebrew/opt/llvm@17/bin')

safety = env.SharedObject("safety.os", "safety.c")
libsafety = env.SharedLibrary("libsafety.so", [safety])

# GCC-style note file is generated by compiler, allow scons to clean it up
env.SideEffect("safety.gcno", safety)
